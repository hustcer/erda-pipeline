#!/usr/bin/env nu
# Author: hustcer
# Created: 2023/06/28 15:33:15
# TODO:
#  [x] æ‰§è¡Œæ–°æµæ°´çº¿ä¹‹å‰å¯ä»¥æŸ¥è¯¢æ˜¯å¦æœ‰æ­£åœ¨è¿è¡Œçš„æµæ°´çº¿ï¼Œå¦‚æœæœ‰åˆ™åœæ­¢æ‰§è¡Œï¼Œä¹Ÿå¯ä»¥åŠ ä¸Š `-f` å¼ºåˆ¶æ‰§è¡Œ
#  [x] æ‰§è¡Œæ–°æµæ°´çº¿ä¹‹å‰å¯ä»¥æŸ¥è¯¢åŒä¸€ Commit æ˜¯å¦å·²ç»è¢«éƒ¨ç½²è¿‡ï¼Œå¦‚æœéƒ¨ç½²è¿‡åˆ™åœæ­¢æ‰§è¡Œï¼Œä¹Ÿå¯ä»¥åŠ ä¸Š `-f` å¼ºåˆ¶æ‰§è¡Œ
#  [x] å…è®¸æŸ¥è¯¢æŸä¸ª target ä¸‹çš„æœ€è¿‘20æ¡æµæ°´çº¿è®°å½•
#  [x] Erda OpenAPI Session è¿‡æœŸåè‡ªåŠ¨ç»­æœŸ
#  [x] è‡ªåŠ¨æ ¹æ®åˆ†æ”¯åç§°æ¨æ–­ç›®æ ‡ç¯å¢ƒæ˜¯å±äº DEV, TEST, STAGING è¿˜æ˜¯ PROD
#  [x] éIDæ–¹å¼æŸ¥è¯¢æµæ°´çº¿åˆ†é¡µç»“æœæ—¶è‡ªåŠ¨è¾“å‡ºæ­£åœ¨æ‰§è¡Œçš„æ‰€æœ‰æµæ°´çº¿çš„è¯¦ç»†ä¿¡æ¯
#  [ ] å…è®¸åœæ­¢æ­£åœ¨æ‰§è¡Œçš„æµæ°´çº¿
# Description: åˆ›å»º Erda æµæ°´çº¿å¹¶æ‰§è¡Œï¼ŒåŒæ—¶å¯ä»¥æŸ¥è¯¢æµæ°´çº¿æ‰§è¡Œç»“æœ

use common.nu [has-ref build-line hr-line log]

const NA = 'N/A'
const ERDA_HOST = 'https://erda.cloud'
const PIPELINE_POLLING_INTERVAL = 3sec

export-env {
  # $env.config.table.mode = 'light'
  # FIXME: å»é™¤å‰å¯¼ç©ºæ ¼èƒŒæ™¯è‰²
  $env.config.color_config.leading_trailing_space_bg = { attr: n }
}

# åˆ¤æ–­æ˜¯å¦éœ€è¦é‡è¯•ï¼Œå¦‚æœè¿”å› true åˆ™é‡è¯•ï¼Œå¦åˆ™ä¸é‡è¯•
def should-retry [resp: any] {
  let isEmpty = ($resp | is-empty)
  let noAuth = ($resp | describe) == 'string' and ($resp =~ 'auth failed' or $resp =~ 'Unauthorized')
  $isEmpty or $noAuth
}

# Check if the required environment variable was set, quit if not
def check-envs [] {
  # éƒ¨ç½²/æŸ¥è¯¢ Pipeline æ“ä½œéœ€è¦å…ˆé…ç½® ERDA_USERNAME & ERDA_PASSWORD
  let envs = ['ERDA_USERNAME' 'ERDA_PASSWORD']
  let empties = ($envs | filter {|it| $env | get -i $it | is-empty })
  if ($empties | length) > 0 {
    print $'Please set (ansi r)($empties | str join ',')(ansi reset) in your environment first...'
    exit 5
  }
}

# Renew Erda session by username and password if expired
export def get-auth [] {
  print 'Renewing Erda session...'
  let query = { username: $env.ERDA_USERNAME, password: $env.ERDA_PASSWORD } | url build-query
  let RENEW_URL = $'https://openapi.erda.cloud/login?($query)'
  mut renew = (curl --silent -X POST $RENEW_URL | from json)
  if ($renew | is-empty) {
    $renew = (curl --silent -X POST $RENEW_URL | from json)
  }
  if ($renew | describe) == 'string' {
    print $'Erda session renew failed with message: (ansi r)($renew)(ansi reset)'; exit 8
  }
  $'cookie: OPENAPISESSION=($renew.sessionid)'
}

# Check if the pipeline config was set correctly, quit if not
def check-pipeline-conf [conf: any] {
  let keys = ['pid', 'appId', 'branch', 'appName', 'pipeline']

  let empties = ($keys | filter {|it| $conf | get -i $it | is-empty })
  if ($empties | length) > 0 {
    print $'Please set (ansi r)($empties | str join ',')(ansi reset) in the following pipeline config:'
    print $conf; exit 1
  }
}

# æ ¹æ® AppIDã€Branchã€Pipeline æŸ¥è¯¢æœ€è¿‘çš„æµæ°´çº¿æ‰§è¡Œè®°å½•
def query-cicd [aid: int, appName: string, branch: string, erdaEnv: string, pipeline: string, count?: int = 20, --auth: string] {
  # Possible env values: DEV,TEST,STAGING,PROD
  let cicd = {
    ymlNames: $'($aid)/($erdaEnv)/($branch)/($pipeline)',
    appID: $aid, branches: $branch, sources: 'dice', pageNo: 1, pageSize: $count
  }
  let cicdUrl = $'($ERDA_HOST)/api/terminus/cicds?($cicd | url build-query)'

  # Query the id of newly created CICD
  mut ci = (curl --silent -H $auth $cicdUrl | from json)
  # Check session expired, and renew if needed
  if (should-retry $ci) {
    $ci = (curl --silent -H (get-auth) $cicdUrl | from json)
  }
  # log 'Query CICD: ' ($ci.data.pipelines | select id commit status | table -e)
  if ($ci | describe) == 'string' or ($ci | is-empty) {
    print $'Query CICD failed in query-cicd with message: (ansi r)($ci)(ansi reset)'; exit 1
  }
  if not $ci.success {
    print $'(ansi r)Query CICD failed, Please try again ...(ansi reset)'
    print ($ci | table -e)
    exit 1
  }
  return $ci
}

# æ ¼å¼åŒ–æµæ°´çº¿æŸ¥è¯¢ç»“æœï¼Œä»¥æ›´å‹å¥½çš„æ–¹å¼å‘ˆç°
def format-pipeline-data [pipelines: list] {
  return (
    $pipelines
      | select -i id commit status normalLabels extra timeBegin timeUpdated filterLabels
      | upsert id {|it| $it | get-pipeline-url }
      | upsert timeBegin {|it| if ($it | get -i timeBegin | is-empty) { $NA } else { $it.timeBegin } }
      | update commit {|it| $it.commit | str substring 0..9 }
      | upsert Comment {|it| $it.normalLabels.commitDetail | from json | get -i comment | str trim }
      | upsert Author {|it| $it.normalLabels.commitDetail | from json | get -i author }
      | update status {|it| $'(ansi pb)($it.status)(ansi reset)' }
      | upsert Runner {|it| $it.extra | get -i runUser | default {name: $NA} | get name }
      | upsert Begin {|it| if $it.timeBegin == $NA { $it.timeBegin } else { $it.timeBegin | into datetime | date humanize } }
      | upsert Updated {|it| $it.timeUpdated | into datetime | date humanize }
      | reject extra timeBegin timeUpdated normalLabels filterLabels
      | rename ID Commit Status
  )
}

# Render pipeline ID as a clickable link while querying latest CICDs
def get-pipeline-url [--as-raw-string] {
  let $pipeline = $in
  let id = $pipeline.id
  let appid = $pipeline.filterLabels.appID
  let pid = $pipeline.filterLabels.projectID
  let link = $'($ERDA_HOST)/terminus/dop/projects/($pid)/apps/($appid)/pipeline/obsoleted?pipelineID=($id)'
  if $as_raw_string { $link } else {
    # FIXME: æ— æ³•æ­£ç¡®æ¸²æŸ“é“¾æ¥, å› ä¸º ansi link åœ¨ extra feature é‡Œé¢
    # $link | ansi link --text $'($id)'
    return $id
  }
}

# æŸ¥è¯¢æŒ‡å®šç›®æ ‡ä¸Šæœ€æ–°çš„Næ¡æµæ°´çº¿æ‰§è¡Œç»“æœ
def query-latest-cicd [pipeline: record, --watch, --auth: string, --show-running-detail] {
  let app = $pipeline
  let environment = $app.environment
  check-envs

  print $'Querying latest CICDs for (ansi pb)($app.appName) on ($app.branch)(ansi reset) branch:'; hr-line -c pb
  let ci = (query-cicd $app.appId $app.appName $app.branch $environment $app.pipeline 10 --auth $auth)
  if ($ci.data.total == 0) {
    print $'No CICD found for (ansi pb)($app.appName)(ansi reset) on (ansi g)($app.branch)(ansi reset) branch'; exit 0
  }
  let pipelines = (format-pipeline-data $ci.data.pipelines)
  print ($pipelines | table -ew 150)
  print 'URL of the latest pipeline:'; hr-line
  print ($ci.data.pipelines | first | get-pipeline-url --as-raw-string)
  print (char nl)
  if ($show_running_detail) {
    let running = $ci.data.pipelines | where status == 'Running'
    if ($running | length) == 0 { return }
    print $'Detail of the running pipelines:'; hr-line
    $running | get ID | each {|it| query-cicd-by-id $it --auth $auth --watch=$watch }
  }
}

# æ£€æŸ¥æ˜¯å¦æœ‰æ­£åœ¨æ‰§è¡Œçš„æµæ°´çº¿ï¼Œå¦‚æœæœ‰åˆ™æ˜¾ç¤ºå…¶æ¦‚è¦ä¿¡æ¯å¹¶é€€å‡º
def check-cicd [aid: int, appName: string, branch: string, erdaEnv: string, pipeline: string, --auth: string] {
  print $'Checking running CICDs for (ansi pb)($appName)(ansi reset) with (ansi g)($pipeline)(ansi reset) from (ansi g)($branch)(ansi reset) branch'
  let ci = (query-cicd $aid $appName $branch $erdaEnv $pipeline --auth $auth)
  if ($ci.data.total == 0) { return true }

  # Update the remote-tracking branches to get the latest commit ID
  # git fetch origin $branch
  # Always use the remote commit id for checking, `str trim` is required here
  let commitID = if (has-ref $'origin/($branch)') { git rev-parse $'origin/($branch)' | str trim } else { '' }
  # Possible pipeline status: Running,Success,Failed,StopByUser
  let running = ($ci.data.pipelines | where status == 'Running')
  # log 'latest' ($ci.data.pipelines | select id commit status)
  let deployed = ($ci.data.pipelines | where commit == $commitID | where status == 'Success')
  let nRunning = ($running | length)
  let nDeployed = ($deployed | length)
  # æ²¡æœ‰æ­£åœ¨éƒ¨ç½²çš„æµæ°´çº¿ï¼Œä¹Ÿæœªæ›¾éƒ¨ç½²è¿‡åˆ™ç›´æ¥è¿”å›ä»¥æ‰§è¡Œä¸‹ä¸€æ­¥
  if $nRunning == 0 and $nDeployed == 0 { return true }
  if $nRunning > 0 {
    print $'There are running pipelines, please wait with patience or re-run with `-f` flag.'
  } else if $nDeployed > 0 {
    print $'The commit (ansi p)($commitID | str substring 0..9)@($branch)(ansi reset) has been deployed, re-run with `-f` flag to deploy it again.'
  }
  let result = if $nRunning > 0 { $running } else { $deployed }
  hr-line 96 -abc pb
  print (format-pipeline-data $result)
  return false
}

# åˆ›å»º CICD æµæ°´çº¿å¹¶è¿”å›å…¶å¯¹åº” ID
def create-cicd [aid: int, appName: string, branch: string, pipeline: string, --auth: string] {
  let cicdUrl = $'($ERDA_HOST)/api/terminus/cicds'
  let cicd = { appID: $aid, branch: $branch, pipelineYmlName: $pipeline }
  print $'Initialize CICD for (ansi pb)($appName)(ansi reset) with (ansi g)($pipeline)(ansi reset) from (ansi g)($branch)(ansi reset) branch'

  # Query the ID of newly created CICD
  mut ci = (curl --silent -H $auth --data-raw $'($cicd | to json)' $cicdUrl | from json)
  # Check session expired, and renew if needed
  if (should-retry $ci) {
    $ci = (curl --silent -H (get-auth) --data-raw $'($cicd | to json)' $cicdUrl | from json)
  }
  if ($ci | describe) == 'string' { print $'Initialize CICD failed with message: (ansi r)($ci)(ansi reset)'; exit 1 }
  if $ci.success { print $'(ansi g)Initialize CICD successfully...(ansi reset)'; return $ci.data.id }
  print $'(ansi r)Initialize CICD failed, Please try again ...(ansi reset)'
  print ($ci | table -e)
  exit 1
}

# æ‰§è¡ŒæŒ‡å®š ID çš„æµæ°´çº¿
def run-cicd [id: int, appid: int, pid: int, --watch, --auth: string] {
  let runUrl = $'($ERDA_HOST)/api/terminus/cicds/($id)/actions/run'
  mut run = (curl --silent -H $auth -X POST $runUrl | from json)
  let url = $'($ERDA_HOST)/terminus/dop/projects/($pid)/apps/($appid)/pipeline/obsoleted?pipelineID=($id)'
  # Check session expired, and renew if needed
  if (should-retry $run) {
    $run = (curl --silent -H (get-auth) -X POST $runUrl | from json)
  }
  if $run.success {
    print $'CICD started, You can query the pipeline running status with id: (ansi g)($id)(ansi reset)'
    print $'Or visit ($url) for more details'
  }
  if $watch { watch-cicd-status $id }
}

# æ ¹æ®æµæ°´çº¿ ID è½®è¯¢æµæ°´çº¿æ‰§è¡Œç»“æœå¹¶æ˜¾ç¤º, è½®è¯¢é—´éš”ä¸º 2 ç§’
export def watch-cicd-status [id: int] {
  let auth = (get-auth)
  let stages = polling-stage-status $id $auth
  let total = $stages | length
  const UNFINISHED_STATUS = [Born, Created, Analyzed, Queue, Running]
  const FINISH_STATUS = [Success, Failed, StopByUser, NoNeedBySystem]
  print $'(char nl)Pipeline Running Detail:'; hr-line

  # pipelineTasks status: Created,Analyzed,Success,Queue,Running,Failed,StopByUser,NoNeedBySystem
  for stage in ($stages | enumerate) {
    let stageStatus = $stage.item.pipelineTasks | get status
    let tasks = $stage.item.pipelineTasks | get name | str join ', '
    let duration = $'($stage.item.pipelineTasks | get costTimeSec | math sum)sec' | into duration
    let stageSuccess = $stageStatus | all {|it| $it == 'Success' }
    let stageFailed = $stageStatus | any {|it| $it == 'Failed' }
    let stageStopped = $stageStatus | any {|it| $it == 'StopByUser' }
    let stageSkipped = $stageStatus | all {|it| $it == 'NoNeedBySystem' }
    let stageUnfinished = $stageStatus | any {|it| $it in $UNFINISHED_STATUS }
    let indicator = if $stageSuccess {
        $'(ansi g)âœ“(ansi reset)  Stage: (ansi g)($tasks)(ansi reset) Finished Successfully! Time cost: ($duration)'
      } else if $stageSkipped {
        $'(ansi y)â˜•(ansi reset) Stage: (ansi y)($tasks)(ansi reset) Was skipped!' # ğŸ’¥ ğŸ’­ ğŸ‘» ğŸ’¨ â˜•
      } else if $stageFailed {
        $'(ansi y)âš (ansi reset)  Stage: (ansi y)($tasks)(ansi reset) Failed! Time cost: ($duration)'
      } else if $stageStopped {
        $'(ansi y)ğŸ‘»(ansi reset) Stage: (ansi y)($tasks)(ansi reset) Was stopped! Time cost: ($duration)'
      } else if $stageUnfinished {
        $'(ansi pb)ğŸª„(ansi reset) Stage: (ansi g)($tasks)(ansi reset) is Running...'
      } else {
        $'(ansi r)âœ—(ansi reset) Unknown Status: ($stageStatus | str join ",")'
      }

    $env.config.table.mode = 'psql'
    print $'Stage ($stage.index + 1)/($total): ($indicator)'
    mut counter = 0
    mut keepPolling = $stageUnfinished
    while $keepPolling {
      $counter += 1
      print (build-line $counter *)  # * ğŸ’¤ ğŸ‘£ âœ¨ ğŸµ âš¡ ğŸ‰ ğŸ”¹ ğŸ”¸
      if ($counter == 90) { $counter = 0; print -n (char nl) }
      let pollingStages = polling-stage-status $id $auth --sid $stage.item.id
      let tasks = $pollingStages | flatten | get pipelineTasks
      let status = $tasks | get status
      if ($status | any {|it| $it in $UNFINISHED_STATUS }) {
        $keepPolling = true
      } else {
        $keepPolling = false
        let duration = $'($tasks | get costTimeSec | math sum)sec' | into duration
        print $'(char nl)Stage finished with status:(char nl)'
        $tasks | select name status | rename Name Status | print
        print $'(char nl)Time cost of this stage: ($duration)'
        hr-line 60 -c grey66
      }
      sleep $PIPELINE_POLLING_INTERVAL
    }
  }
  # Refresh the query result and print the final costTimeSec
  let query = fetch-cicd-detail $id $auth
  let totalTime = $'($query.data.costTimeSec)sec' | into duration
  print $'(char nl)Pipeline run finished with status: (ansi p)($query.data.status)(ansi reset)! Total time cost: ($totalTime)'
}

# æŸ¥è¯¢æµæ°´çº¿æ‰§è¡Œç»“æœçš„ç›¸åº”é˜¶æ®µçš„è¯¦ç»†ä¿¡æ¯
def polling-stage-status [id: int, auth: string, --sid: int] {
  let query = fetch-cicd-detail $id $auth
  const PIPELINE_TASK_COLUMNS = [id name type status costTimeSec queueTimeSec timeBegin timeEnd extra]
  # pipelineTasks status: Created,Success,Queue,Running,Failed,StopByUser
  let stages = $query.data.pipelineStages
    | select id pipelineTasks
    | upsert pipelineTasks {|it| $it.pipelineTasks | select ...$PIPELINE_TASK_COLUMNS }
  let stages = if not ($sid | is-empty) { $stages | where id == $sid } else { $stages }
  $stages
}

# æŸ¥è¯¢æµæ°´çº¿æ‰§è¡Œç»“æœçš„è¯¦ç»†ä¿¡æ¯
export def fetch-cicd-detail [id: int, auth: string, --host: string = $ERDA_HOST] {
  let queryUrl = $'($host)/api/terminus/pipelines/($id)'
  mut query = (curl --silent -H $auth $queryUrl | from json)

  # Check session expired, and renew if needed
  loop {
    if (should-retry $query) {
      $query = (curl --silent -H $auth $queryUrl | from json)
    } else { break }
  }
  $query
}

# æ ¹æ®æµæ°´çº¿ ID æŸ¥è¯¢æµæ°´çº¿æ‰§è¡Œç»“æœ
def query-cicd-by-id [id: int, --watch, --auth: string] {
  let queryUrl = $'($ERDA_HOST)/api/terminus/pipelines/($id)'
  mut query = (curl --silent -H $auth $queryUrl | from json)

  # Check session expired, and renew if needed
  if (should-retry $query) {
    $query = (curl --silent -H (get-auth) $queryUrl | from json)
  }
  if ($query | describe) == 'string' { print $'Query CICD by id failed with message: (ansi r)($query)(ansi reset)'; exit 1 }
  if (not $query.success ) { print $'Query CICD failed with error message: (ansi r)($query.err.msg)(ansi reset)'; exit 1 }
  let timeEnd = if ($query.data.timeEnd | is-empty) { $'(ansi wd)---Not Yet!---(ansi reset)' } else { $query.data.timeEnd }

  let output = {
    App: $query.data.applicationName
    Branch: $query.data.branch
    Status: $'(ansi pb)($query.data.status)(ansi reset)'
    Runner: $query.data.extra.runUser.name
    Committer: $query.data.commitDetail.author
    Commit: ($query.data.commit | str substring 0..9)
    Comment: ($query.data.commitDetail.comment | str trim)
    Begin: $query.data.timeBegin
    End: $timeEnd
    Duration: ($'($query.data.costTimeSec)sec' | into duration)
    # æ­¤å¤„ä¹‹æ‰€ä»¥æ²¡æœ‰ç›´æ¥ç”¨ $appid & $pid æ˜¯å› ä¸ºå¯èƒ½å­˜åœ¨åœ¨ A åº”ç”¨ä»“åº“ä¸­æŸ¥è¯¢ B åº”ç”¨çš„æµæ°´çº¿æ‰§è¡Œç»“æœçš„æƒ…å†µï¼Œæ•…è€Œä»¥è¿”å›æ•°æ®ä¸ºå‡†
    URL: $'($ERDA_HOST)/terminus/dop/projects/($query.data.projectID)/apps/($query.data.applicationID)/pipeline/obsoleted?pipelineID=($id)'
  }
  print $'(char nl)(ansi pb)Current Running Status of CICD ($id):(ansi reset)'
  print '----------------------------------------------------------'
  print $output
  # print ($query | table -e)     # Just for debugging purpose
  if $watch { watch-cicd-status $id }
}

# åˆ›å»º Erda æµæ°´çº¿å¹¶æ‰§è¡Œï¼ŒåŒæ—¶å¯ä»¥æŸ¥è¯¢æµæ°´çº¿æ‰§è¡Œç»“æœ
export def main [
  operation: string,      # ç›®å‰æ”¯æŒä¸¤ç§æ“ä½œç±»å‹ï¼Œrun å’Œ query, run ç”¨äºåˆ›å»ºå¹¶æ‰§è¡Œ CICD, query ç”¨äºæŸ¥è¯¢ CICD æ‰§è¡Œç»“æœ
  pipeline?: record,      # å½“æ“ä½œä¸º run æ—¶å¿…é¡»æŒ‡å®šï¼Œç”¨äºæŒ‡å®šæµæ°´çº¿çš„é…ç½®ä¿¡æ¯
  --watch(-w),            # æŒç»­è½®è¯¢å¹¶æ˜¾ç¤ºæ­£åœ¨æ‰§è¡Œçš„æµæ°´çº¿çš„è¯¦ç»†ä¿¡æ¯
  --auth(-a): string,     # APIè°ƒç”¨çš„æˆæƒä¿¡æ¯
  --force(-f),            # å½“æ“ä½œä¸º run æ—¶ç”Ÿæ•ˆï¼Œå³ä¾¿å·²ç»æœ‰æ­£åœ¨è¿è¡Œçš„æµæ°´çº¿æˆ–è€…å·²ç»éƒ¨ç½²è¿‡ä¹Ÿä¼šå¼ºåˆ¶é‡æ–°æ‰§è¡Œ
  --cid(-i): int,         # å½“æ“ä½œä¸º query æ—¶ç”Ÿæ•ˆï¼Œç”¨äºæŸ¥è¯¢ CICD æ‰§è¡Œç»“æœï¼Œå¦‚æœä¸ä¼ åˆ™æŸ¥è¯¢æœ€è¿‘ 10 æ¡æµæ°´çº¿æ‰§è¡Œç»“æœ
] {
  check-envs

  match $operation {
    run | r => {
      # æ ¹æ®æµæ°´çº¿ ID æŸ¥è¯¢æ— éœ€åŠ è½½å…¶ä»–ç¯å¢ƒå˜é‡ï¼Œä¹Ÿä¸éœ€è¦ .termixrc æ–‡ä»¶
      let isIdQuery = ($operation in ['query', 'q']) and ($cid > 0)
      check-pipeline-conf $pipeline
      let app = $pipeline

      # ä»¥ä¸‹ä¸ºåº”ç”¨çº§åˆ«é…ç½®ï¼Œåº”ç”¨çš„æ‰€æœ‰å¼€å‘è€…ä¿æŒä¸€è‡´ï¼Œå¯ä»¥æ”¾åœ¨ä»£ç ä»“åº“é‡Œé¢
      let pid = $app.pid
      let appid = $app.appId
      let branch = $app.branch
      let appName = $app.appName
      let environment = $app.environment
      # æ£€æŸ¥æ˜¯å¦æœ‰æ­£åœ¨æ‰§è¡Œçš„æµæ°´çº¿ä»¥åŠæ˜¯å¦è¯¥ Commit å·²ç»éƒ¨ç½²è¿‡
      if not $force {
        if not (check-cicd $appid $appName $branch $environment $app.pipeline --auth $auth) { return }
      }
      let cicdid = (create-cicd $appid $appName $branch $app.pipeline --auth $auth)
      run-cicd ($cicdid | into int) $appid $pid --auth $auth --watch=$watch

    }
    query | q => {
      # æœªæŒ‡å®š cid åˆ™æŸ¥è¯¢æœ€è¿‘ 10 æ¡æµæ°´çº¿æ‰§è¡Œç»“æœ
      if ($cid | is-empty) {
        check-pipeline-conf $pipeline
        query-latest-cicd $pipeline --auth $auth --watch=$watch --show-running-detail; exit 0
      }
      if ($cid | describe) != 'int' {
        print $'Invalid value for --cid: (ansi r)($cid)(ansi reset), should be an integer number.'; exit 1
      }
      query-cicd-by-id $cid --auth $auth --watch=$watch
    }
    _ => {
      print $'Unsupported operation: (ansi r)($operation)(ansi reset), should be (ansi g)run(ansi reset) or (ansi g)query(ansi reset)'
      exit 1
    }
  }
}

# åˆ›å»º Erda æµæ°´çº¿å¹¶æ‰§è¡Œï¼Œé»˜è®¤æƒ…å†µä¸‹ä¼šæ£€æŸ¥æ˜¯å¦æœ‰æµæ°´çº¿æ­£åœ¨æ‰§è¡Œæˆ–è€…æ˜¯å¦è¯¥ Commit å·²ç»éƒ¨ç½²è¿‡ï¼Œè‹¥æœ‰åˆ™åœæ­¢å¹¶ç»™äºˆæç¤º
export def erda-deploy [
  pipeline: record,       # æŒ‡å®šå¾…æ‰§è¡Œçš„æµæ°´çº¿çš„é…ç½®ä¿¡æ¯
  --watch(-w),            # æ‰§è¡Œæµæ°´çº¿æ—¶æŒç»­è½®è¯¢å¹¶æ˜¾ç¤ºè¯¥æµæ°´çº¿å„ä¸ª Stage çš„è¯¦ç»†æ‰§è¡Œä¿¡æ¯
  --auth(-a): string,     # APIè°ƒç”¨çš„æˆæƒä¿¡æ¯
  --force(-f),            # å³ä¾¿å·²ç»æœ‰æ­£åœ¨è¿è¡Œçš„æµæ°´çº¿ï¼Œæˆ–è€…å³ä¾¿è¯¥ Commit å¯¹åº”çš„åˆ†æ”¯å·²ç»éƒ¨ç½²è¿‡ä¹Ÿä¼šå¼ºåˆ¶é‡æ–°éƒ¨ç½²
] {
  main run $pipeline --force=$force --auth $auth --watch=$watch
}

# æ ¹æ®æµæ°´çº¿ ID æˆ–ç›®æ ‡ç¯å¢ƒæŸ¥è¯¢æµæ°´çº¿æ‰§è¡Œç»“æœ, ä¾‹å¦‚: å•åº”ç”¨: t dq 997636681239659; t dq test, å¤šåº”ç”¨: t dq dev -a all
export def erda-query [
  pipeline?: record,      # æŒ‡å®šå¾…æŸ¥è¯¢çš„æµæ°´çº¿çš„é…ç½®ä¿¡æ¯
  --watch(-w),            # æŒç»­è½®è¯¢å¹¶æ˜¾ç¤ºæŒ‡å®šæµæ°´çº¿å„ä¸ª Stage çš„è¯¦ç»†æ‰§è¡Œä¿¡æ¯
  --auth(-a): string,     # APIè°ƒç”¨çš„æˆæƒä¿¡æ¯
  --cid(-i): any,         # ç”¨äºé€šè¿‡æµæ°´çº¿çš„æ‰§è¡Œ ID æŸ¥è¯¢ CICD æ‰§è¡Œç»“æœï¼Œå¦‚æœæŒ‡å®šè¯¥å‚æ•°åˆ™å¿½ç•¥ dest å‚æ•°
] {
  # å…è®¸éæŒ‡å®šæµæ°´çº¿IDçš„æŸ¥è¯¢
  if ($cid | is-empty) {
    main query $pipeline --auth $auth --watch=$watch
  } else {
    main query --cid $cid --auth $auth --watch=$watch
  }
}
